buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'base'
}

clean.doFirst {
    delete "${rootDir}/build"
    println "${rootDir}/build"
}

subprojects {
    configurations {
        publishArchives
    }

    repositories {
        jcenter()
    }

    apply plugin: 'java'

    group 'ai.h2o'

    archivesBaseName = project.name
    if (project.findProperty('archivesNameSuffix')) {
        archivesBaseName += "-${project.findProperty('archivesNameSuffix')}"
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }

    javadoc {
        options.locale = 'en_US'
    }

    task checkJavaVersion << {
        def prop = System.getenv('CHECK_JAVA_VERSION');
        if (prop != "false" && !JavaVersion.current().isJava7()) {
            String message = "ERROR: Java 7 required but " +
                    JavaVersion.current() +
                    " found. Change your JAVA_HOME environment variable. " +
                    "For local development, you can set environment variable CHECK_JAVA_VERSION=false to bypass the check."
            throw new IllegalStateException(message)
        }
    }
    compileJava.dependsOn checkJavaVersion

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        publishArchives sourcesJar
        publishArchives javadocJar
    }
}
